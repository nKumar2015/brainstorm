use crate::ast::{
    Expression, 
    Program, 
    Statement, 
    Operator,
    ListItem, 
    ForLoop,
    IfBranch,
    UserClass,
    ClassInitDef,
    ClassField,
    ClassMethod,
};

use std::collections::HashMap;
use crate::value::Value;

grammar;

pub Program: Program = {
    <statements:Statement*> => Program::Body{statements}
}

pub Statement: Statement = {
    "import" <path:StringLiteral> ";" => Statement::Import{path},

    <lhs:Expression> "=" <rhs:Expression> ";"
        => Statement::Assignment{lhs, rhs},
    
    <name:Identifier> <operator:AssignmentOperator> <rhs:Expression> ";" 
        => Statement::OperatorAssignment{name, operator, rhs},
        
    <params:ForLoop> => Statement::For{params},
    <params:IfBranch> => Statement::If{params}, 

    "while" "(" <condition:Expression> ")" "{" <statements:Statement*> "}" 
        => Statement::While{condition, statements},
    
    <expression:Expression> ";" => Statement::Expression{expression},

    "fn" <name:Identifier> "(" <arguments:FunctionDefArgs> ")" 
    "{" <statements:Statement*> "return" <return_expression:Expression> ";" "}"
        => Statement::FunctionDefinition{name, arguments, statements, return_expression: Some(return_expression)},

    "fn" <name:Identifier> "(" <arguments:FunctionDefArgs> ")" 
    "{" <statements:Statement*> "}"
        => Statement::FunctionDefinition{name, arguments, statements, return_expression: None},    
}

pub FunctionDefArgs: Vec<String> = {
    () => vec![],
    <argument:Identifier> => vec![argument],
    <argument:Identifier> "," <tail:FunctionDefArgs> => {
        let mut args = tail.clone();
        args.insert(0, argument);

        args
    }
}

pub ClassFields: HashMap<String, ClassField> = {
    () => HashMap::new(),
    <visibility:"pub"?> <field:Identifier> => {
        let mut fields = HashMap::new();
        let new_field = ClassField{is_private: visibility.is_some(), value: Value::Null};
        fields.insert(field, new_field);

        fields
    },
    <visibility:"pub"?> <field:Identifier> "," <tail:ClassFields> => {
        let mut fields = tail.clone();
        let new_field = ClassField{is_private: visibility.is_some(), value: Value::Null};
        fields.insert(field, new_field);

        fields
    }
}

pub ClassInit: ClassInitDef = {
    () => ClassInitDef{name: None, arguments: None, statements: None},
    "init" "(" <arguments:FunctionDefArgs> ")"
    "{" <statements:Statement*> "}" ";"
        => ClassInitDef{name: Some("default_init".to_string()), arguments: Some(arguments), statements: Some(statements)}
}

pub ClassMethods: HashMap<String, ClassMethod> = {
    () => HashMap::new(),
    <visibility:"pub"?> "fn" <name:Identifier> "(" <arguments:FunctionDefArgs> ")" 
    "{" <statements:Statement*> "return" <return_expression:Expression> ";" "}" => {
        let mut methods = HashMap::new();
        let method = ClassMethod{arguments, statements, 
                                 return_exp: Some(return_expression), 
                                 is_private: visibility.is_some() };
        
        methods.insert(name, method);

        methods
    },
    <visibility:"pub"?> "fn" <name:Identifier> "(" <arguments:FunctionDefArgs> ")" 
    "{" <statements:Statement*> "}" => {
        let mut methods = HashMap::new();
        let method = ClassMethod{arguments, statements, 
                                 return_exp: None, 
                                 is_private: visibility.is_some() };

        methods.insert(name, method);

        methods
    },
    <visibility:"pub"?> "fn" <name:Identifier> "(" <arguments:FunctionDefArgs> ")" 
    "{" <statements:Statement*> "return" <return_expression:Expression> ";" "}" ";" <tail:ClassMethods> => {
        let mut methods = tail.clone();
        let method = ClassMethod{arguments, statements, 
                                 return_exp: Some(return_expression), 
                                 is_private: visibility.is_some() };

        methods.insert(name, method);

        methods
    },
    <visibility:"pub"?> "fn" <name:Identifier> "(" <arguments:FunctionDefArgs> ")" 
    "{" <statements:Statement*>"}" ";" <tail:ClassMethods> => {
        let mut methods = tail.clone();
        let method = ClassMethod{arguments, statements, 
                                 return_exp: None, 
                                 is_private: visibility.is_some() };

        methods.insert(name, method);

        methods
    }
}

pub elif_branches: (Vec<Expression>, Vec<Vec<Statement>>) = {
    "elif" "(" <condition:Expression> ")"
    "{" <statements:Statement*>"}" => (vec![condition], vec![statements]),

    "elif" "(" <condition:Expression> ")"
    "{" <statements:Statement*>"}" <tail:elif_branches> => {
        let(mut conds, mut stmnts) = tail.clone();

        conds.insert(0, condition);
        stmnts.insert(0, statements);

        (conds, stmnts)
    }

}

pub IfBranch: IfBranch = {
    "if" "(" <condition:Expression> ")" 
    "{" <statements:Statement*> "}"
    <elif_data:elif_branches>
        => IfBranch{condition, 
                    statements, 
                    else_statements: None,
                    elif_data },

    "if" "(" <condition:Expression> ")" 
    "{" <statements:Statement*> "}" 
    <elif_data:elif_branches>
    "else" "{" <else_statements:Statement*> "}" 
        => IfBranch{condition, 
                    statements, 
                    else_statements: Some(else_statements),
                    elif_data },   

    "if" "(" <condition:Expression> ")" 
    "{" <statements:Statement*> "}"
        => IfBranch{condition, 
                    statements, 
                    else_statements: None,
                    elif_data: (vec![], vec![]) },

    "if" "(" <condition:Expression> ")" 
    "{" <statements:Statement*> "}"
    "else" "{" <else_statements:Statement*> "}" 
        => IfBranch{condition, 
                    statements, 
                    else_statements: Some(else_statements),
                    elif_data: (vec![], vec![]) },
}

pub ForLoop: ForLoop = {
    "for" <loop_var:Identifier> "in" <iterate_expression:Expression> 
    "{" <statements:Statement*> "}"
        => ForLoop{loop_var, iterate_expression, statements}
}

pub Expression: Expression = {
    #[precedence(level="0")]
    <v:IntLiteral>       => Expression::Int{v},
    <name:Identifier>    => Expression::Identifier{name},
    <s:StringLiteral>    => Expression::String{s},
    <b:BooleanLiteral>   => Expression::Boolean{b},
    <f:FloatLiteral>     => Expression::Float{f},
    <c:CharacterLiteral> => Expression::Character{c},

    "class" <name:Identifier> "{" 
    "fields" "{" <fields:ClassFields> "}" ";"
    <init:ClassInit>
    <methods:ClassMethods> "}"
        => Expression::ClassDef{params: Box::new(UserClass{name, fields, init, methods})},
    
    "new" <class_name:Identifier> "(" <arguments:FunctionArgs> ")" 
        => Expression::ObjectCreation{class_name, arguments},

    <name:Identifier> "." <method:Identifier> "(" <arguments:FunctionArgs> ")"
        => Expression::MethodCall{name, method, arguments},

    <obj:Identifier> "." <field:Identifier>
        => Expression::FieldAccess{name: obj, field},

    "[" <items:ExpressionList> "]" => Expression::List{items},

    <function:Identifier> "(" <arguments:FunctionArgs> ")" 
        => Expression::Call{function, arguments},

    "++" <name:Identifier> 
        => Expression::Prefix{name, 
                              operator: Operator::Plus, 
                              rhs: Box::new(Expression::Int{v: 1}) },
                                
    "--" <name:Identifier>
        => Expression::Prefix{name, 
                              operator: Operator::Minus, 
                              rhs: Box::new(Expression::Int{v: 1}) },
    
    <name:Identifier> "[" <idx_exp:Expression> "]"
        => Expression::Index{name, idx_exp: Box::new(idx_exp)},

    #[precedence(level="1")] #[assoc(side="left")]
    <lhs:Expression> <operator:Operator> <rhs:Expression>
        => Expression::Operation{lhs: Box::new(lhs), 
                                 rhs: Box::new(rhs), 
                                 operator },
                        
    "[" <iterate_exp:Expression> "for" <var:Identifier> "in" <control_exp:Expression> "]" 
        => Expression::Comprehension{iterate_exp: Box::new(iterate_exp), var, control_exp: Box::new(control_exp)},

}

pub FunctionArgs: Vec<Expression> = {
    () => vec![],
    <argument:Expression> => vec![argument],
    <argument:Expression> "," <tail:FunctionArgs> => {
        let mut args = tail.clone();
        args.insert(0, argument);

        args
    }
}

pub ExpressionList: Vec<ListItem> = {
    () => vec![],
    <pack:".."?> <head:Expression> <spread:".."?> => vec![ListItem{
        expression: head, 
        is_spread: spread.is_some(),
        is_pack: pack.is_some(),
    }],
    <head:Expression> <spread:".."?> "," <tail:ExpressionList> => {
        let mut list = tail.clone();
        list.insert(0, ListItem{
            expression: head, 
            is_spread: spread.is_some(),
            is_pack: false,
        });

        list
    },
}

Operator: Operator = {
    <s:"+">  => Operator::Plus,
    <s:"-">  => Operator::Minus,
    <s:"*">  => Operator::Times,
    <s:"/">  => Operator::Divide,
    <s:"<">  => Operator::LessThan,
    <s:">">  => Operator::GreaterThan,
    <s:"=="> => Operator::Equal,
    <s:"!="> => Operator::NotEqual,
}

AssignmentOperator: Operator = {
    <s:"+="> => Operator::Plus,
    <s:"-="> => Operator::Minus,
    <s:"*="> => Operator::Times,
    <s:"/="> => Operator::Divide,
}

Identifier: String = {
    <s:r"[_a-zA-Z][_a-zA-Z0-9]*"> => s.to_string(),
}

IntLiteral: i32 = {
    <s:r"[0-9]+"> => s.parse().unwrap(),
    <s:r"-[0-9]+"> => s.parse().unwrap(),
}

FloatLiteral: f64 = {
    <s:r"[0-9]+[.][0-9]+"> => s.parse().unwrap(),
}

StringLiteral: String = {
    <s:r#""[^\"]*""#> => s.replace('"', "").to_string(),
}

BooleanLiteral: bool = {
    <s:"true"> => true,
    <s:"false"> => false,
}

CharacterLiteral: char = {
    <s:r#"'[^\']'"#> => s.replace('\'', "").chars().next().expect("invalid char!"),
}

match {
    r"//[^\n\r]*[\n\r]*" => {},
    r"\s*" => {},
    _,
}